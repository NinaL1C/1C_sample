
#Область ОбработчикиСобытийФормы 

&НаСервере
Процедура ЛН_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	Если Объект.ОплатаПоЗаявкам Тогда   
		УстановитьНеДоступностьЭлементовФормы(ЭтаФорма, КоманднаяПанель.ПодчиненныеЭлементы);
	КонецЕсли;  
КонецПроцедуры 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура УстановитьНеДоступностьЭлементовФормы(Форма, ЭлементыКоманднойПанели)  
	
	СписокКнопокКоманднойПанели = Новый СписокЗначений;
	СформироватьСписокКнопокКоманднойПанели(СписокКнопокКоманднойПанели, ЭлементыКоманднойПанели);
	
	Для Каждого Элемент Из Форма.Элементы Цикл
		Если СписокКнопокКоманднойПанели.НайтиПоЗначению(Элемент.Имя) <> Неопределено Тогда 
			Элемент.Доступность = Истина;
			Продолжить;
		КонецЕсли;
		Если ТипЗнч(Элемент) = Тип("КнопкаФормы") Тогда   
			Элемент.Доступность = Ложь; 
		Иначе
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(Элемент, "ТолькоПросмотр") Тогда
				Элемент.ТолькоПросмотр = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;   
	
КонецПроцедуры  

&НаСервереБезКонтекста
Процедура СформироватьСписокКнопокКоманднойПанели(СписокКнопокКоманднойПанели, ЭлементыКоманднойПанели)
	
	Для Каждого Элемент Из ЭлементыКоманднойПанели Цикл 
		Если ТипЗнч(Элемент) = Тип("ГруппаФормы") Тогда
			СформироватьСписокКнопокКоманднойПанели(СписокКнопокКоманднойПанели, Элемент.ПодчиненныеЭлементы);			
		Иначе
			Если ТипЗнч(Элемент) = Тип("КнопкаФормы") Тогда 
				СписокКнопокКоманднойПанели.Добавить(Элемент.Имя);  
			КонецЕсли;
		КонецЕсли;  
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
